buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.0'
        classpath 'org.ajoberstar:gradle-git:1.2.0'
        classpath 'com.badlogicgames.gdx:gdx-tools:1.6.4'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '0.1.1'
    ext {
        appName = 'Overlap2D'
        gdxVersion = '1.6.2'
        box2DLightsVersion = '1.4'
        visuiVersion = '0.7.6'

        packMain = ["art/textures", "assets/style/", "uiskin"]
        packSplash = ["art/splash_textures", "assets/splash/", "splash"]
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "http://repo.serkprojects.com/content/groups/public/" }
        jcenter()
    }
}

apply plugin: "java"


import org.ajoberstar.grgit.*
def spineDestination = file("spine-runtimes")
if(!spineDestination.exists()) Grgit.clone(dir: spineDestination, uri: 'https://github.com/EsotericSoftware/spine-runtimes.git')
def o2dRuntimeDestination = file("overlap2d-runtime-libgdx")
if(!o2dRuntimeDestination.exists()) Grgit.clone(dir: o2dRuntimeDestination, uri: 'https://github.com/UnderwaterApps/overlap2d-runtime-libgdx.git')

dependencies {
    compile "com.kotcrab.vis:vis-ui:$visuiVersion"
    compile project(":overlap2d-runtime-libgdx")
    compile project(":spine-runtime-libgdx")
    compile project(":spine-o2d-extension")
    compile fileTree(dir: 'libs', include: '*.jar')
    compile "org.apache.commons:commons-io:1.3.2"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-jglfw:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
    compile "com.mortennobel:java-image-scaling:0.8.5"
    compile "net.mountainblade:modular:1.0-SNAPSHOT"
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker;
task packTextures << {
  if (project.ext.has('packMain')) {
    logger.info "Calling TexturePacker: "+packMain
    TexturePacker.processIfModified(file(packMain[0]).absolutePath, file(packMain[1]).absolutePath, packMain[2])
  }
  if (project.ext.has('packSplash')) {
    logger.info "Calling TexturePacker: "+packSplash
    TexturePacker.processIfModified(file(packSplash[0]).absolutePath, file(packSplash[1]).absolutePath, packSplash[2])
  }
}
build.dependsOn packTextures

project(":overlap2d-runtime-libgdx") {
    apply plugin: "java"
    sourceSets.main.java.srcDirs = ["src/"]

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile fileTree(dir: 'libs', include: '*.jar')
    }
}


project(":spine-o2d-extension") {
    apply plugin: "java"
    sourceSets.main.java.srcDirs = ["src/"]

    dependencies {
        compile project(":spine-runtime-libgdx");
        compile project(":overlap2d-runtime-libgdx");
        compile fileTree(dir: 'libs', include: '*.jar')
    }
}

project(":spine-runtime-libgdx") {
    apply plugin: "java"
    sourceSets.main.java.srcDirs = ["src/"]

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}


sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = ["src/"]

project.ext.mainClassName = "com.uwsoft.editor.Main"
project.ext.assetsDir = new File("assets/");


task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.assetsDir);

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

idea {
    project {
        languageLevel = sourceCompatibility
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
    doLast {
        def classpath = new XmlParser().parse(file(".classpath"))
        new Node(classpath, "classpathentry", [kind: 'src', path: 'assets']);
        def writer = new FileWriter(file(".classpath"))
        def printer = new XmlNodePrinter(new PrintWriter(writer))
        printer.setPreserveWhitespace(true)
        printer.print(classpath)
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
